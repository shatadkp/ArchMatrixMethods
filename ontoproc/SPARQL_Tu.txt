PREFIX : <http://api.stardog.com/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?shift ?status (ROUND(SUM(?durationMinutes)) AS ?totalMinutes)
WHERE {
  VALUES (?shiftStart ?shiftEnd ?shift) {
    ("2024-07-04T00:00:00Z"^^xsd:dateTime "2024-07-04T08:00:00Z"^^xsd:dateTime "Shift 1")
    ("2024-07-04T08:00:00Z"^^xsd:dateTime "2024-07-04T16:00:00Z"^^xsd:dateTime "Shift 2")
    ("2024-07-04T16:00:00Z"^^xsd:dateTime "2024-07-05T00:00:00Z"^^xsd:dateTime "Shift 3")
  }

  ?machineStatus :has_participant :USA0640_5290_PR5OkumaCNCGrinder_Grinder ;
                 :occupies_temporal_region ?interval ;
                 rdfs:label ?statusLabel .

  ?interval :start_time ?intervalStart ;
            :end_time ?intervalEnd .

  BIND(IF(?intervalStart < ?shiftStart, ?shiftStart, ?intervalStart) AS ?overlapStart)
  BIND(IF(?intervalEnd > ?shiftEnd, ?shiftEnd, ?intervalEnd) AS ?overlapEnd)

  FILTER(?overlapStart < ?overlapEnd)

  BIND((?overlapEnd - ?overlapStart) AS ?duration)
  BIND((HOURS(?duration) * 60 + MINUTES(?duration) + SECONDS(?duration) / 60) AS ?durationMinutes)

  BIND(
    IF(CONTAINS(UCASE(STR(?statusLabel)), "MAINTENANCE"), "MAINTENANCE",
    IF(CONTAINS(UCASE(STR(?statusLabel)), "IDLE"), "IDLE",
    IF(CONTAINS(UCASE(STR(?statusLabel)), "SETUP"), "SETUP",
    IF(CONTAINS(UCASE(STR(?statusLabel)), "RUNNING"), "RUNNING",
    "OTHER")))) AS ?status
  )
}
GROUP BY ?shift ?status
ORDER BY ?shift ?status
